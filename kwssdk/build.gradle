apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

version = properties.getProperty("project.version")
group = properties.getProperty("project.group")

def siteUrl = 'https://github.com/SuperAwesomeLTD/sa-kws-android-sdk'
def gitUrl = 'https://github.com/SuperAwesomeLTD/sa-kws-android-sdk.git'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "KWSSDK"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Kids Web Services Mobile SDK for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'gabrielcoman'
                        name 'Gabriel Coman'
                        email 'gabriel.coman@superawesome.tv'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }

}

repositories {
    maven { url  "http://dl.bintray.com/gabrielcoman/maven" }
    mavenCentral()
}

dependencies {
    compile 'tv.superawesome.lib.sajsonparser:sajsonparser:0.2.7'
    compile 'tv.superawesome.lib.sanetwork:sanetwork:0.3.8'
    compile 'tv.superawesome.lib.sautils:sautils:0.3.5'

    // SuperAwesome base library
    implementation 'tv.superawesome.samobilebase:samobilebase:2.1.1'

    // SuperAwesome Protobufs
    implementation 'tv.superawesome.protobufs:protobufs:1.4.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // tests
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.13.0"
    testImplementation "eu.codearte.catch-exception:catch-exception:1.4.4"
    testImplementation "org.assertj:assertj-core:3.8.0"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation files('libs/json.jar')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Set this dependency if you want to use Hamcrest matching
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'


}